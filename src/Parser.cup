import java_cup.runtime.*;
import java.util.ArrayList;
import java.lang.reflect.Field;
import java.util.Arrays;

scan with {: return lexer.next_token(); :};

parser code {:

  static class ErrorInfo {
    public int line;
    public int column;

    public ErrorInfo(int line, int column) {
      this.line = line;
      this.column = column;
    }
  }

  static class Node {
      public ArrayList<Node> children;
      private static int globalCounter = 0;

      public int counter;
      private Object value;

      public Node(Object value){
        Node.globalCounter += 1;
        this.counter = Node.globalCounter;
        this.value = value;
        this.children = new ArrayList<Node>();
      }

      public void addChild(Node n){ children.add(n); }

      public String toString(){
        String ret = "";
        ret += String.format("%d [label=\"%s\"];\n",
                              counter, value.toString());
        for(Node n: children){
          ret += String.format("%d -> %d;\n", this.counter, n.counter);
          ret += n.toString();
        }
        return ret;
      }
    }

  Lexer lexer;

  // used by SC.java to decide whether parsing is successful
  public boolean syntaxErrors;

  public Parser(Lexer lex) {
      super(lex);
      lexer = lex;
  }

  public void syntax_error(Symbol cur_token) {
    this.syntaxErrors = true;
  }

  public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
    this.syntaxErrors = true;
    report_fatal_error(
      "Syntax error at line " + (cur_token.left+1) + ", column "
      + cur_token.right + ".\n", null
    );
  }

  public void custom_err_report(String message, int line, int column)
    throws java.lang.Exception
  {
    this.syntaxErrors = true;
    report_fatal_error( message + "\n", new ErrorInfo(line, column) );
  }

:};

terminal ILLEGAL_CHARACTER, FDEF, TDEF, MAIN, LPAREN, RPAREN, LCURL, RCURL, SEMICOL, PRINT, STRING, PRIMTYPE, IDENTIFIER, ASSIGN, INTEGER, CHAR, RETURN, FLOAT, RATIONAL, SEQ, RANGLE, LANGLE, LSQUARE, RSQUARE, COMMA, PLUS, MINUS, MULT, DIV, NOT, AND, OR, IF, TRUE, FALSE, EQUAL, LEQ, NEQ, ELSE, WHILE, BREAK, FOR, OF, RANGE, THREAD, WAIT, DICT, COLON, QUEST, TOP;

nonterminal program, statements, semicolon_statement, statement, expression, datatype, aggregate_datatype, sequence, sequence_items, definitions, function, parameters, parameter_items, number, main, argument_items, arguments, bool_not, comp_less, comp_leq, comp_neq, comp_equal, value, bool_and, bool_or, maths_factor, maths_term, variable, range, range_val, dictionary, dictionary_items, variable_identifier, comp_in, definition, typedef;


program ::= definitions:ds main:m {:
        Node n = new Node("PROGRAM");
        n.addChild((Node)ds);
        n.addChild((Node)m);

        // System.out.println("digraph G {");
        // System.out.println(n);
        // System.out.println("}");

        RESULT = n;
    :};

definitions ::=
    /* empty */ {: RESULT = new Node("DEFINITIONS"); :} |
    definitions:ds definition:d SEMICOL {:
        Node n = (Node)ds;
        n.addChild((Node)d);
        RESULT = n;
    :};

definition ::=
    function:f {: RESULT = f; :} |
    typedef:td {: RESULT = td; :} |
    variable:v {: RESULT = v; :};

/* TODO prevent main having return values */
main ::=
    FDEF MAIN LPAREN RPAREN LCURL statements:ss RCURL SEMICOL {:
        Node n = new Node("MAIN");
        n.addChild((Node)ss);
        RESULT = n;
    :};

function ::=
    FDEF IDENTIFIER:i parameters:ps LCURL statements:ss RCURL {:
        Node n = new Node("FUNCTION");
        n.addChild((Node)ps);
        n.addChild((Node)ss);
        RESULT = n;
    :} |
    FDEF datatype:dt IDENTIFIER:i parameters:ps LCURL statements:ss RCURL {:
        Node n = new Node("FUNCTION");
        n.addChild((Node)dt);
        n.addChild((Node)ps);
        n.addChild((Node)ss);
        RESULT = n;
    :};

parameters ::= LPAREN parameter_items:pis RPAREN {: RESULT = pis; :};

parameter_items ::=
    /* empty */ {: RESULT = new Node("PARAMETERS"); :} |
    datatype:dt expression:e {:
        Node n = new Node("PARAMETERS");
        n.addChild((Node)dt);
        n.addChild((Node)e);
        RESULT = n;
    :} |
    parameter_items:pis COMMA datatype:dt expression:e {:
        Node n = (Node)pis;
        n.addChild((Node)dt);
        n.addChild((Node)e);
        RESULT = n;
    :};

typedef ::=
    TDEF IDENTIFIER:i LCURL parameter_items:pis RCURL {:
        Node n = new Node("TDEF");
        n.addChild(new Node(i));
        n.addChild((Node)pis);
        RESULT = n;
    :};

arguments ::= LPAREN argument_items:ais RPAREN {: RESULT = ais; :};

argument_items ::=
    /* empty */ {: RESULT = new Node("ARGUMENTS"); :} |
    expression:e {:
        Node n = new Node("ARGUMENTS");
        n.addChild((Node)e);
        RESULT = n;
    :} |
    argument_items:ais COMMA expression:e {:
        Node n = (Node)ais;
        n.addChild((Node)e);
        RESULT = n;
    :};

statements ::=
    statements:ss statement:s {:
        Node n = (Node)ss;
        n.addChild((Node)s);
        RESULT = n;
    :} |
    /* empty */ {:
        RESULT = new Node("STATEMENTS");
    :};

semicolon_statement ::=
    PRINT expression:e {:
        Node n = new Node("PRINT");
        n.addChild((Node)e);
        RESULT = n;
    :} |
    variable:v {: RESULT = (Node)v; :} |
    typedef:td {: RESULT = (Node)td; :} |
    RETURN {: RESULT = new Node("RETURN"); :} |
    RETURN expression:e {:
        Node n = new Node("RETURN");
        n.addChild((Node)e);
        RESULT = n;
    :} |
    BREAK {: RESULT = new Node("BREAK"); :} |
    BREAK INTEGER:i {:
        Node n = new Node("BREAK");
        n.addChild(new Node(i));
        RESULT = n;
    :} |
    THREAD variable_identifier:vi ASSIGN LCURL statements:ss RCURL {:
        Node n = new Node("THREAD");
        n.addChild((Node)vi);
        n.addChild((Node)ss);
        RESULT = n;
    :} |
    WAIT LPAREN variable_identifier:vi RPAREN {:
        Node n = new Node("WAIT");
        n.addChild((Node)vi);
        RESULT = n;
    :};

variable ::=
    datatype:dt variable_identifier:vi {:
        Node n = new Node("DECLARATION");
        n.addChild((Node)dt);
        n.addChild((Node)vi);
        RESULT = n;
    :} |
    datatype:dt variable_identifier:vi ASSIGN expression:e {:
        Node n = new Node("INITIALISATION");
        n.addChild((Node)dt);
        n.addChild((Node)vi);
        n.addChild((Node)e);
        RESULT = n;
    :} |
    variable_identifier:vi ASSIGN expression:e {:
        Node n = new Node("ASSIGNMENT");
        n.addChild((Node)vi);
        n.addChild((Node)e);
        RESULT = n;
    :};

statement ::=
    semicolon_statement:ss SEMICOL {: RESULT = (Node)ss; :} |
    IF LPAREN expression:e RPAREN LCURL statements:ss1 RCURL ELSE LCURL statements:ss2 RCURL {:
        Node n = new Node("IF");
        n.addChild((Node)e);
        n.addChild((Node)ss1);
        n.addChild((Node)ss2);
        RESULT = n;
    :} |
    IF LPAREN expression:e RPAREN LCURL statements:ss RCURL {:
        Node n = new Node("IF");
        n.addChild((Node)e);
        n.addChild((Node)ss);
        RESULT = n;
    :} |
    WHILE LCURL statements:ss RCURL {:
        Node n = new Node("WHILE");
        n.addChild((Node)ss);
        RESULT = n;
    :};

datatype ::=
    PRIMTYPE:dt {: RESULT = new Node(dt); :} |
    SEQ LANGLE aggregate_datatype:dt RANGLE {:
        Node n = new Node("SEQUENCE");
        n.addChild((Node)dt);
        RESULT = n;
    :} |
    DICT LANGLE aggregate_datatype:dt1 COMMA aggregate_datatype:dt2 RANGLE {:
        Node n = new Node("DICTIONARY");
        n.addChild((Node)dt1);
        n.addChild((Node)dt2);
        RESULT = n;
    :};

aggregate_datatype ::=
    datatype:dt {: RESULT = (Node)dt; :} |
    TOP {: RESULT = new Node("TOP"); :};

sequence ::= LSQUARE sequence_items:sis RSQUARE {: RESULT = sis; :};

sequence_items ::=
    /* empty */ {: RESULT = new Node("SEQUENCE"); :} |
    expression:e {:
        Node n = new Node("SEQUENCE");
        n.addChild((Node)e);
        RESULT = n;
    :} |
    sequence_items:sis COMMA expression:e {:
        Node n = (Node)sis;
        n.addChild((Node)e);
        RESULT = n;
    :};

dictionary ::= LPAREN dictionary_items:dis RPAREN {: RESULT = dis; :};

dictionary_items ::=
    /* empty */ {: RESULT = new Node("DICTIONARY"); :} |
    expression:e1 COLON expression:e2 {:
        Node n = new Node("DICTIONARY");
        n.addChild((Node)e1);
        n.addChild((Node)e2);
        RESULT = n;
    :} |
    dictionary_items:dis COMMA expression:e1 COLON expression:e2 {:
        Node n = (Node)dis;
        n.addChild((Node)e1);
        n.addChild((Node)e2);
        RESULT = n;
    :};

expression ::=
    LSQUARE expression:e FOR IDENTIFIER:i OF range:r RSQUARE {:
        Node n = new Node("LISTCOMP");
        n.addChild((Node)e);
        n.addChild((Node)r);
        RESULT = n;
    :} |
    LSQUARE expression:e1 FOR IDENTIFIER:i OF range:r COMMA IF expression:e2 RSQUARE {:
        Node n = new Node("LISTCOMP");
        n.addChild((Node)e1);
        n.addChild((Node)r);
        n.addChild((Node)e2);
        RESULT = n;
    :} |
    bool_or:bo {: RESULT = (Node)bo; :};

range ::=
    RANGE LPAREN range_val:rv RPAREN {:
        Node n = new Node("RANGE");
        n.addChild((Node)rv);
        RESULT = n;
    :} |
    RANGE LPAREN range_val:rv1 COMMA range_val:rv2  RPAREN {:
        Node n = new Node("RANGE");
        n.addChild((Node)rv1);
        n.addChild((Node)rv2);
        RESULT = n;
    :} |
    RANGE LPAREN range_val:rv1 COMMA range_val:rv2 COMMA range_val:rv3  RPAREN {:
        Node n = new Node("RANGE");
        n.addChild((Node)rv1);
        n.addChild((Node)rv2);
        n.addChild((Node)rv3);
        RESULT = n;
    :};

range_val ::=
    INTEGER: i {: RESULT = new Node(i); :} |
    variable_identifier: vi {: RESULT = vi; :};

bool_or ::=
    bool_or:bo OR bool_and:ba {:
        Node n = new Node("OR");
        n.addChild((Node)bo);
        n.addChild((Node)ba);
        RESULT = n;
    :} |
    bool_and:ba {: RESULT = (Node)ba; :};

bool_and ::=
    bool_and:ba AND bool_not:bn {:
        Node n = new Node("AND");
        n.addChild((Node)ba);
        n.addChild((Node)bn);
        RESULT = n;
    :} |
    bool_not:bn {: RESULT = (Node)bn; :};

bool_not ::=
    NOT bool_not:bn {:
        Node n = new Node("NOT");
        n.addChild((Node)bn);
        RESULT = n;
    :} |
    comp_in:ci {: RESULT = (Node)ci; :};

comp_in ::=
    comp_in:ci QUEST comp_less:cle {:
        Node n = new Node("IN");
        n.addChild((Node)ci);
        n.addChild((Node)cle);
        RESULT = n;
    :} |
    comp_less: cle {: RESULT = (Node) cle; :};

comp_less ::=
    comp_less:cle LANGLE comp_leq:cleq {:
        Node n = new Node("LESSTHAN");
        n.addChild((Node)cle);
        n.addChild((Node)cleq);
        RESULT = n;
    :} |
    comp_leq:cleq {: RESULT = (Node)cleq; :};

comp_leq ::=
    comp_leq:cl LEQ comp_neq:cn {:
        Node n = new Node("LESSOREQUAL");
        n.addChild((Node)cl);
        n.addChild((Node)cn);
        RESULT = n;
    :} |
    comp_neq:cn {: RESULT = (Node)cn; :};

comp_neq ::=
    comp_neq:cn NEQ comp_equal:ce {:
        Node n = new Node("NOTEQUAL");
        n.addChild((Node)cn);
        n.addChild((Node)ce);
        RESULT = n;
    :} |
    comp_equal:ce {: RESULT = (Node)ce; :};

comp_equal ::=
    comp_equal:ce EQUAL maths_factor:mf {:
        Node n = new Node("EQUAL");
        n.addChild((Node)ce);
        n.addChild((Node)mf);
        RESULT = n;
    :} |
    maths_factor:mf {: RESULT = (Node)mf; :};

maths_factor ::=
    maths_factor:mf PLUS maths_term:mt {:
        Node n = new Node("PLUS");
        n.addChild((Node)mf);
        n.addChild((Node)mt);
        RESULT = n;
    :} |
    maths_factor:mf MINUS maths_term:mt {:
        Node n = new Node("MINUS");
        n.addChild((Node)mf);
        n.addChild((Node)mt);
        RESULT = n;
    :} |
    maths_term:mt {: RESULT = (Node)mt; :};

maths_term ::=
    maths_term:mt MULT value:v {:
        Node n = new Node("MULT");
        n.addChild((Node)mt);
        n.addChild((Node)v);
        RESULT = n;
    :} |
    maths_term:mt DIV value:v {:
        Node n = new Node("DIV");
        n.addChild((Node)mt);
        n.addChild((Node)v);
        RESULT = n;
    :} |
    value:v {: RESULT = (Node)v; :};

value ::=
    INTEGER:i        {: RESULT = new Node(i); :} |
    MINUS INTEGER:i        {:
        Integer mi = Integer.parseInt("-" + i.toString());
        RESULT = new Node(mi);
    :} |
    FLOAT:f          {: RESULT = new Node(f); :} |
    MINUS FLOAT:f          {:
        Float mf = Float.parseFloat("-" + f.toString());
        RESULT = new Node(mf);
    :} |
    RATIONAL:r       {: RESULT = new Node(r); :} |
    MINUS RATIONAL:r       {: RESULT = new Node("-" + r); :} |
    CHAR:c           {: RESULT = new Node(c); :} |
    STRING:s         {: RESULT = new Node(s); :} |
    number:n         {: RESULT = new Node(n); :} |
    sequence:s       {: RESULT = s; :} |
    dictionary:d     {: RESULT = d; :} |
    TRUE             {: RESULT = new Node("TRUE"); :} |
    FALSE            {: RESULT = new Node("TRUE"); :} |
    variable_identifier:vi arguments:as {:
        Node n = new Node("FUNCTION CALL");
        n.addChild((Node)vi);
        n.addChild((Node)as);
        RESULT = n;
    :} |
    variable_identifier:vi {: RESULT = (Node)vi; :} |
    LPAREN expression:e RPAREN {: RESULT = (Node)e; :};

variable_identifier ::=
    IDENTIFIER:i {: RESULT = new Node(i); :} |
    variable_identifier:vi1 LSQUARE variable_identifier:vi2 RSQUARE {:
        Node n = new Node("IDENTIFIER");
        n.addChild((Node)vi1);
        n.addChild((Node)vi2);
        RESULT = n;
    :} |
    variable_identifier:vi LSQUARE INTEGER:i RSQUARE {:
        Node n = new Node("IDENTIFIER");
        n.addChild((Node)vi);
        n.addChild(new Node(i));
        RESULT = n;
    :};