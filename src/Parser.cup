import java_cup.runtime.*;
import java.util.ArrayList;

scan with {: return lexer.next_token(); :};

parser code {:

  static class ErrorInfo {
    public int line;
    public int column;

    public ErrorInfo(int line, int column) {
      this.line = line;
      this.column = column;
    }
  }

  static class Node {
      public ArrayList<Node> children;
      private static int globalCounter = 0;

      public int counter;
      private Object value;

      public Node(Object value){
        Node.globalCounter += 1;
        this.counter = Node.globalCounter;
        this.value = value;
        this.children = new ArrayList<Node>();
      }

      public void addChild(Node n){ children.add(n); }

      public String toString(){
        String ret = "";
        ret += String.format("%d [label=\"%s\"];\n",
                              counter, value.toString());
        for(Node n: children){
          ret += String.format("%d -> %d;\n", this.counter, n.counter);
          ret += n.toString();
        }
        return ret;
      }
    }

  Lexer lexer;

  // used by SC.java to decide whether parsing is successful
  public boolean syntaxErrors;

  public Parser(Lexer lex) {
      super(lex);
      lexer = lex;
  }

  public void syntax_error(Symbol cur_token) {
    this.syntaxErrors = true;
  }

  public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
    this.syntaxErrors = true;
    report_fatal_error(
      "Syntax error at line " + (cur_token.left+1) + ", column "
      + cur_token.right + ".\n", null
    );
  }

  public void custom_err_report(String message, int line, int column)
    throws java.lang.Exception
  {
    this.syntaxErrors = true;
    report_fatal_error( message + "\n", new ErrorInfo(line, column) );
  }

:};

terminal ILLEGAL_CHARACTER, FDEF, MAIN, LPAREN, RPAREN, LCURL, RCURL, SEMICOL, PRINT, STRING, PRIMTYPE, IDENTIFIER, ASSIGN, INTEGER, CHAR, RETURN, FLOAT, RATIONAL, SEQ, RANGLE, LANGLE, LSQUARE, RSQUARE, COMMA;

nonterminal program, statements, statement, value, optional_return, datatype, sequence, sequence_items;

program ::=
    FDEF MAIN LPAREN RPAREN LCURL statements:ss optional_return RCURL SEMICOL {:
        Node n = new Node("MAIN");
        n.addChild((Node)ss);

        // System.out.println("digraph G {");
        // System.out.println(n);
        // System.out.println("}");

        RESULT = n;
    :};

optional_return ::=
    /* empty */ |
    RETURN SEMICOL;

statements ::=
    statements:ss statement:s SEMICOL {:
        Node n = (Node)ss;
        n.addChild((Node)s);
        RESULT = n;
    :} |
    /* empty */ {:
        RESULT = new Node("STATEMENTS");
    :};

statement ::=
    PRINT STRING:s {:
        Node n = new Node("PRINT");
        n.addChild(new Node(s));
        RESULT = n;
    :} |
    datatype:dt IDENTIFIER:id ASSIGN value:v {:
        Node n = new Node("ASSIGNMENT");
        n.addChild((Node)dt);
        n.addChild(new Node(id));
        n.addChild((Node)v);
        RESULT = n;
    :};

datatype ::=
    PRIMTYPE:dt {: RESULT = new Node(dt); :} |
    SEQ LANGLE datatype:dt RANGLE {:
        Node n = new Node("SEQUENCE");
        n.addChild((Node)dt);
        RESULT = n;
    :};

sequence ::= LSQUARE sequence_items:sis RSQUARE {: RESULT = sis; :};

sequence_items ::=
    /* empty */ {: RESULT = new Node("SEQUENCE"); :} |
    value:v {:
        Node n = new Node("SEQUENCE");
        n.addChild((Node)v);
        RESULT = n;
    :} |
    sequence_items:sis COMMA value:v {:
        Node n = (Node)sis;
        n.addChild((Node)v);
        RESULT = n;
    :};

value ::=
    INTEGER:i    {: RESULT = new Node(i); :} |
    CHAR:c       {: RESULT = new Node(c); :} |
    IDENTIFIER:i {: RESULT = new Node(i); :} |
    FLOAT:f      {: RESULT = new Node(f); :} |
    RATIONAL:r   {: RESULT = new Node(r); :} |
    sequence:s   {: RESULT = s; :};
